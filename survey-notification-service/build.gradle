plugins {
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

configurations {
	asciidoctorExt
}

ext {
	set('springCloudVersion', "2023.0.3")
	set('snippetsDir', file("build/generated-snippets"))
}
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// jpa
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// MySQL JDBC 드라이버
	implementation 'org.mariadb.jdbc:mariadb-java-client:3.2.0'

	//redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// asciidoctor
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	implementation 'net.bytebuddy:byte-buddy:1.12.4'

}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	outputs.dir snippetsDir
}

tasks.register("restDocsTest", Test) {
	outputs.dir snippetsDir
	useJUnitPlatform {
		includeTags("restDocs")
	}

	finalizedBy "asciidoctor"
}

tasks.named("asciidoctor") {
	dependsOn restDocsTest

	inputs.dir snippetsDir
	configurations "asciidoctorExt"
	baseDirFollowsSourceDir()
}

tasks.register('copyAsciidocHTML', Copy) {
	dependsOn asciidoctor
	from "${asciidoctor.outputDir}"
	into "${project.rootDir}/src/main/resources/static/docs"
}
